/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2015 Eugen [WebDucer] Richter
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Version of the module
version = "0.1.2-beta1"

android {
   compileSdkVersion 22
   buildToolsVersion '22.0.1'
   resourcePrefix 'wdutils'

   defaultConfig {
      minSdkVersion 10
      targetSdkVersion 22
      versionCode 2
      versionName version
   }
   buildTypes {
      release {
         minifyEnabled false
         proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
   }
   compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
   }
   productFlavors {
   }
}

// Maven variables
def siteUrl = 'https://bitbucket.org/WebDucer/android-utilities/wiki/Home'
def gitUrl = 'https://bitbucket.org/WebDucer/android-utilities.git'
def issuesUrl = 'https://bitbucket.org/WebDucer/android-utilities/issues'
group = "de.webducer.android"

install {
   repositories.mavenInstaller {
      // This generates POM.xml with proper parameters
      pom {
         project {
            packaging 'aar'

            artifactId 'wd-android-utilities'

            // Add your description here
            name 'Utilities for android applications'
            url siteUrl

            // Set your license
            licenses {
               license {
                  name 'The MIT License (MIT)'
                  url 'http://opensource.org/licenses/MIT'
               }
            }
            developers {
               developer {
                  id 'WebDucer'
                  name 'Eugen Richter'
                  email 'android@webducer.de'
               }
            }
            scm {
               connection gitUrl
               developerConnection gitUrl
               url siteUrl

            }
         }
      }
   }
}

task sourcesJar(type: Jar) {
   from android.sourceSets.main.java.srcDirs
   classifier = 'sources'
}

task javadoc(type: Javadoc) {
   source = android.sourceSets.main.java.srcDirs
   classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
   classifier = 'javadoc'
   from javadoc.destinationDir
}
artifacts {
   archives javadocJar
   archives sourcesJar
}

// upload artifact to Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
   user = properties.getProperty("bintray.user")
   key = properties.getProperty("bintray.apikey")

   // dryRun = true

   configurations = ['archives']
   pkg {
      repo = "WD-Android-Libraries"
      name = "WD-Android-Utilities"
      issueTrackerUrl = issuesUrl
      websiteUrl = siteUrl
      vcsUrl = gitUrl
      licenses = ["MIT"]
      labels = ['Android', 'WebDucer', 'Utilities']
      publish = true
      publicDownloadNumbers = true
   }
}
